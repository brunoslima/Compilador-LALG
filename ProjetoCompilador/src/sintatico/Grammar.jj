options
{
  
}
PARSER_BEGIN(Grammar)
package sintatico;

import java.util.ArrayList;

public class Grammar
{
public static String descricao = "";

static void error_skipto(int kind) {
  ParseException e = generateParseException();  // generate the exception object.
  //System.out.println(e.toString());  // print the error message
  Token t;
  do {
    t = getNextToken();
    if (t.kind == 0) return;
  } while (t.kind != kind);

}

static void error_skipto_before(int kind) {
  ParseException e = generateParseException();  // generate the exception object.
  //System.out.println(e.toString());  // print the error message
  Token t;
  do {
    t = getNextToken();
    t.next = Token.newToken(getToken(1).kind, getToken(1).image);
    if (t.next.kind == 0) return;
  } while (t.next == null || t.next.kind != kind);

}

static void error_skipto(Integer... kind) {
    ArrayList<Integer> lista = new ArrayList();
    for(Integer e: kind){
        lista.add(e);
    }
    Token t;
    do {
        t = getNextToken();
        if (t.kind == 0) return;
    } while (!lista.contains(t.kind));
}

static void error_skipto_before(Integer... kind) {
  ArrayList<Integer> lista = new ArrayList();
  for(Integer e: kind){
        lista.add(e);
  }
  Token t;
  do {
    t = getNextToken();
    t.next = Token.newToken(getToken(1).kind, getToken(1).image);
    if (t.next.kind == 0) return;
  } while (!lista.contains(t.next.kind));
}

static void showMessageError(int line, int column, String message, String descricao){
  
    String erro = "ERRO - " + message + " Linha: " + line + " Coluna: " + column + "\n";
    RecuperacaoErros.adicionarErro(erro);
  
    System.out.println("\n=========================\n" +
        message + "\n" +
        "Linha: " + line + " Coluna: " + column + "\n" +
        descricao
    );
}

}
PARSER_END(Grammar)


TOKEN:
{

    < PROGRAM: "program" >
|   < BEGIN: "begin" >
|   < END: "end" >
|   < PROCEDURE: "procedure" >
|   < VAR: "var" >

|   < INT: "int" >
|   < REAL: "real" >
|   < BOOLEAN: "boolean" >
|   < TRUE: "true" >
|   < FALSE: "false" >

|   < READ: "read" >
|   < WRITE: "write" >

|   < DOIS_PONTOS: ":" >
|   < PONTO_VIRGULA: ";" >
|   < VIRGULA: "," >
|   < PONTO: "." >

|   < IF: "if" >
|   < THEN: "then" >
|   < ELSE: "else" >

|   < WHILE: "while" >
|   < DO: "do" >

|   < SIMBOLO_IGUAL: "=" >
|   < SIMBOLO_DIFERENTE: "<>" >
|   < SIMBOLO_MENOR: "<" >
|   < SIMBOLO_MENOR_IGUAL: "<=" >
|   < SIMBOLO_MAIOR: ">" >
|   < SIMBOLO_MAIOR_IGUAL: ">=" >

|   < OP_ATRIBUICAO: ":=" >
|   < OP_ADICAO: "+" >
|   < OP_SUBTRACAO: "-" >
|   < OP_DIVISAO: "div" >
|   < OP_MULTIPLICACAO: "*" >
|   < OP_AND: "and" >
|   < OP_OR: "or" >
|   < OP_NOT: "not" >

|   < PARENTESES_ABRE: "(" >
|   < PARENTESES_FECHA: ")" >


|   < NUMERO_INTEIRO: (["0"-"9"]){1,10} >
|   < NUMERO_REAL:  (["0"-"9"])+ ( "." (["0"-"9"]){1,6} ) >


|   < IDENTIFICADOR: ["_", "A"-"Z", "a"-"z"](["_", "A"-"Z", "a"-"z"] | (["0"-"9"]) )* >


}

SKIP :
{
    " "
|   "\r"
|   "\t"
|   "\n"
|   <"{" (~["}"] )* "}">
|   <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>

}

void compilationUnit() :
{}
{
    try{
        programDeclaration() 
    }
    catch(ParseException e){
        {showMessageError(token.next.beginLine, token.next.beginColumn, "Palavra reservada program esperada", Grammar.descricao);}
        error_skipto(PONTO_VIRGULA);
    }

    try{
        bloco()
        {token.next = Token.newToken(getToken(1).kind, getToken(1).image);}
    }
    catch(ParseException e){
        System.out.println("Não deveria passar nunca aqui");
    }

    try{
        <PONTO>
    }
    catch(ParseException e){
        showMessageError(e.currentToken.beginLine, e.currentToken.beginColumn, "Ponto final faltando", Grammar.descricao);
    }

    try{
        <EOF>
    }
    catch(ParseException e){
        Token t;
        do{
            t = getNextToken();
            showMessageError(token.beginLine, token.beginColumn, "Símbolo inesperado", Grammar.descricao);
        } while(t.kind != EOF);
    }
}

void programDeclaration() throws ParseException:
{}
{
    (
        <PROGRAM>
    |
        {throw generateParseException();}
    )
    
    try{
        <IDENTIFICADOR>
    }
    catch (ParseException e){
        showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Identificador de programa faltando", Grammar.descricao);
        error_skipto(PONTO_VIRGULA);
        return;
    }
    
     
    try{
        <PONTO_VIRGULA>
    }
    catch(ParseException e){
        showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Ponto e vírgula faltando", Grammar.descricao);
        error_skipto_before(INT, BOOLEAN, REAL, PROCEDURE, BEGIN);
    }

}

void bloco() :
{}
{   
    
    [
        //LOOKAHEAD(tipo() <IDENTIFICADOR> (<PONTO_VIRGULA>|<VIRGULA>) )
        parteDeclaracaoVariaveis()
    ]
    (
        LOOKAHEAD(<PROCEDURE> <IDENTIFICADOR> <PARENTESES_ABRE> | <IDENTIFICADOR> <IDENTIFICADOR> <PARENTESES_ABRE> | <PROCEDURE>)
        parteDeclaracaoSubRotinas() 
        
        (
            <PONTO_VIRGULA>
        |
            {showMessageError(token.next.beginLine, token.next.beginColumn, "Ponto e vírgula faltando", Grammar.descricao);}
        )
    )*

    comandoComposto()
}

void parteDeclaracaoVariaveis():
{}
{
    ( 

        {
            if (getToken(1).kind == IDENTIFICADOR && getToken(2).kind == IDENTIFICADOR && getToken(3).kind == PARENTESES_ABRE ||
                getToken(1).kind == IDENTIFICADOR && getToken(2).kind == PARENTESES_ABRE ) {
            
                    return;
            }
        }
        try{
            declaracaoVariaveis() 
        }
        catch(ParseException e){
            error_skipto(PONTO_VIRGULA);
        }

        (
            {
            if (getToken(1).kind == IDENTIFICADOR && getToken(2).kind == IDENTIFICADOR && getToken(3).kind == PARENTESES_ABRE ||
                getToken(1).kind == IDENTIFICADOR && getToken(2).kind == PARENTESES_ABRE ||
                getToken(1).kind == PROCEDURE) {
            
                    return;
                }
            }
            try{
                declaracaoVariaveis()
            }
            catch(ParseException e){
                error_skipto(PONTO_VIRGULA);
            }
        )* 
    )
   
}

void declaracaoVariaveis() throws ParseException:
{}
{

    
    tipo() 
    listaIdentificadores()
    (
        LOOKAHEAD(<PONTO_VIRGULA>)
        <PONTO_VIRGULA>
        |
        {showMessageError(token.next.beginLine, token.next.beginColumn, "Ponto e vírgula faltando", Grammar.descricao);}
    )
}

void listaIdentificadores() :
{}
{ 
    ( 
        variavel() 
        (
            
            LOOKAHEAD(<VIRGULA><IDENTIFICADOR>|<IDENTIFICADOR>|<VIRGULA>)
            (   <VIRGULA>
                |
                {showMessageError(token.beginLine, token.beginColumn, "Vírgula faltando", Grammar.descricao);}
            )
            variavel()
        )* 
    )
}



void parteDeclaracaoSubRotinas() :
{}
{   
    
    try{
        <PROCEDURE>

        try{
            <IDENTIFICADOR>
        }
        catch(ParseException e){
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Identificador de procedure esperado", Grammar.descricao);
            error_skipto_before(PONTO_VIRGULA, PARENTESES_ABRE);
        }
    }
    catch(ParseException e){
        showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Palavra reservada procedure esperada", Grammar.descricao);
        error_skipto_before(PONTO_VIRGULA, PARENTESES_ABRE);
  
    }

    declaracaoProcedimentos() 

}



void declaracaoProcedimentos() :
{}
{    
    ( 
        [
            LOOKAHEAD(<PARENTESES_ABRE>|<VAR>|<IDENTIFICADOR><VIRGULA>|<IDENTIFICADOR><DOIS_PONTOS>)
            try{
                <PARENTESES_ABRE>
            }
            catch(ParseException e){
                showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Abre parentese faltando", Grammar.descricao);
            }

            parametrosFormais() 

            try{
                <PARENTESES_FECHA>
            }
            catch(ParseException e){
                showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Fecha parenteses faltando", Grammar.descricao);
            }
        ] 

        try{
            <PONTO_VIRGULA>
        }
        catch(ParseException e){
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Ponto e vírgula faltando", Grammar.descricao);
        }
        bloco() 
    ) 

}

void parametrosFormais() : 
{}
{
    
    secaoParametrosFormais() 
    (
        try{
            <PONTO_VIRGULA>
        }
        catch(ParseException e){
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Ponto e vírgula faltando", Grammar.descricao);
        } 
        secaoParametrosFormais()
    )*

}

void secaoParametrosFormais() :
{}
{
    ( 
        [
            try{
                <VAR>
            }
            catch(ParseException e){
                showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Palavra reservada var esperada", Grammar.descricao);
            }
        ] 

        try{
            listaIdentificadores() 
        }
        catch(ParseException e){
            
        }

        {error_skipto_before(DOIS_PONTOS, INT, REAL, BOOLEAN, PARENTESES_FECHA, PONTO_VIRGULA);}
        
        try{
            <DOIS_PONTOS>
        }
        catch(ParseException e){
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Dois pontos faltando", Grammar.descricao);
        }
        
        tipo() 
        
    )
}

void tipo() throws ParseException:
{}
{

    
    <INT> 
    | 
    <BOOLEAN> 
    | 
    <REAL>
    |
    (
        {
            showMessageError(token.next.beginLine, token.next.beginColumn, "Identificador de tipo esperado", Grammar.descricao);
            throw generateParseException();
        }
    )
    
}

///////////////////////////////////////
///////////////////////////////////////
///////////////////////////////////////

void comandoComposto():
{}
{
    ( 
        try{
            <BEGIN> 
        }
        catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Palavra reservada begin esperada", Grammar.descricao);
        }

        try{
            comando() 
        }
        catch(ParseException e){
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Comando estruturado incorretamente", Grammar.descricao);
            if (getToken(1).kind == BEGIN) {
                error_skipto(END);
            }
        }
        {
            if (getToken(1).kind == EOF || getToken(1).kind == PONTO) {
                return;
            }
        }
        (
        LOOKAHEAD(<END>|<PONTO_VIRGULA>|comando())
        try{
            (
                <END>
                {return;}
            |
                <PONTO_VIRGULA>
            )
        }
        catch(ParseException e){

            showMessageError(token.next.beginLine, token.next.beginColumn, "Comando estruturado incorretamente; ponto e vírgula faltando ou palavra reservada end faltando", Grammar.descricao);
            error_skipto_before(PONTO_VIRGULA, END);
        }

        try{
            comando() 
        }
        catch(ParseException e){
            //showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Comando estruturado incorretamente", Grammar.descricao);
            
            if (getToken(1).kind == BEGIN) {
                error_skipto_before(END);
            }
        }

        {
            if (getToken(1).kind == EOF || getToken(1).kind == PONTO) {
                showMessageError(token.next.beginLine, token.next.beginColumn, "Comando estruturado incorretamente; ponto e vírgula faltando ou palavra reservada end faltando", Grammar.descricao);
                return;
            }
        
        }
        )*


    )

}

void comando() throws ParseException:
{}
{   
    LOOKAHEAD(<IDENTIFICADOR> <PARENTESES_ABRE> expressao() <PARENTESES_FECHA> <PONTO_VIRGULA>)
    (
        
        chamadaProcedimento() 
    )
    | 
    LOOKAHEAD(<BEGIN>)
    (
        comandoComposto() 
    )
    | 
    LOOKAHEAD(<IF>)
    (    
        comandoCondicional() 
    )
    | 
    LOOKAHEAD(<WHILE>)
    (
        comandoRepetitivo()
    )
    |
    LOOKAHEAD(<READ>)
    ( 
        try{
            <READ> 
        }
        catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Palavra reservada read esperada", Grammar.descricao);
        }
        try{
            <PARENTESES_ABRE> 
        }
        catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Abre parenteses esperado", Grammar.descricao);
        }
        
        listaIdentificadores() 
        try{
            <PARENTESES_FECHA> 
        }
        catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Fecha parenteses esperado", Grammar.descricao);
        }
    )
    | 
    LOOKAHEAD(<WRITE>)
    ( 
        try{
            <WRITE> 
        }
        catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Palavra reservada write esperada", Grammar.descricao);
        }
        try{
            <PARENTESES_ABRE> 
        }
        catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Abre parenteses esperado", Grammar.descricao);
        }
        listaIdentificadores() 
        try{
            <PARENTESES_FECHA> 
        }
        catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Fecha parenteses esperado", Grammar.descricao);
        }
        
    )
    |
    LOOKAHEAD(<IDENTIFICADOR> <OP_ATRIBUICAO> | <IDENTIFICADOR> <DOIS_PONTOS>)
    (
        
        atribuicao()
    )
    |
    (
        {throw generateParseException();}
    )

}

void atribuicao() throws ParseException:
{}
{

    ( 
        variavel() 
        try{
            <OP_ATRIBUICAO >
        }
        catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Operador de atribuição esperado", Grammar.descricao);
        }
        
        (   
            LOOKAHEAD(<PONTO_VIRGULA>|<END>)
            (
                    {showMessageError(token.next.beginLine, token.next.beginColumn, "Faltou informar o que será atribuido", Grammar.descricao);
                    error_skipto_before(PONTO_VIRGULA,END);}
            )
            |
            expressao() 
        )
    )

}

void chamadaProcedimento() :
{}
{

    ( 
        try{
            <IDENTIFICADOR> 
        }
        catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Identificador de procedimento esperado", Grammar.descricao);
        }
        try{
            <PARENTESES_ABRE> 
        }
        catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Abre parenteses esperado", Grammar.descricao);
        }
        listaExpressoes() 
        try{
            <PARENTESES_FECHA> 
        }
        catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Fecha parenteses esperado", Grammar.descricao);
        }
    )

}

void comandoCondicional() throws ParseException:
{}
{
    (
        <IF>
        |
        {showMessageError(token.next.beginLine, token.next.beginColumn, "Palavra reservada IF esperada", Grammar.descricao);
        if (getToken(1).kind != BEGIN) {
            error_skipto_before(BEGIN, PONTO_VIRGULA);
        }
        throw generateParseException();}
    )
    
    expressao()
    
    (
        <THEN>
        |
        {showMessageError(token.next.beginLine, token.next.beginColumn, "Palavra reservada THEN esperada", Grammar.descricao);
        if (getToken(1).kind != BEGIN) {
            error_skipto_before(BEGIN, PONTO_VIRGULA);
        }
        throw generateParseException();}
    ) 
    
    comando() 
    
    
    [ LOOKAHEAD(1) <ELSE> comando()]

}

void comandoRepetitivo() throws ParseException:
{}
{
    try{
        <WHILE>    
    }
    catch(ParseException e){
        showMessageError(token.next.beginLine, token.next.beginColumn, "Palavra reservada WHILE esperada", Grammar.descricao);
        throw generateParseException();
    }
    

    expressao()
    
    try{
        <DO>
    }
    catch(ParseException e){
        showMessageError(token.next.beginLine, token.next.beginColumn, "Palavra reservada DO esperada", Grammar.descricao);
        if (getToken(1).kind != BEGIN) {
            error_skipto_before(IDENTIFICADOR, BEGIN, IF, WHILE, READ, WRITE);
        }
        //throw generateParseException();
    }
        
    

    comando() 
    
}


void expressao() :
{}
{   
    try{
        expressaoSimples() 
    }
    catch(ParseException e){

    }
    
    [
        LOOKAHEAD(relacao() expressaoSimples() | expressaoSimples() | relacao())
        try{
            relacao()
        }
        catch(ParseException e){ }
    

        expressaoSimples()

    
    ]

    
    
        
}

void expressaoSimples() :
{}
{
    [
  
        <OP_ADICAO>
        |
        <OP_SUBTRACAO>
 
    ]

    
    termo()

    (
        //LOOKAHEAD(<OP_ADICAO> | <OP_SUBTRACAO> | <OP_OR>)
        LOOKAHEAD(<OP_ADICAO> | <OP_SUBTRACAO> | <OP_OR> | termo() relacao() | termo() <PONTO_VIRGULA>)
        (
            try{
                (<OP_ADICAO> | <OP_SUBTRACAO> | <OP_OR>)
            }catch(ParseException e){
        
                showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Operador lógico ou aritmético esperado", Grammar.descricao);
            } 
        ) 
        termo()
    )*
}

void relacao() :
{}
{
    (
        try{
            <SIMBOLO_IGUAL> | <SIMBOLO_DIFERENTE> | <SIMBOLO_MENOR> | <SIMBOLO_MENOR_IGUAL> | <SIMBOLO_MAIOR_IGUAL> | <SIMBOLO_MAIOR>
        }catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Operador lógico ou aritmético esperado", Grammar.descricao);
        } 
    )
}

void termo() :
{}
{
    try{
        fator()
    }
    catch(ParseException e){
        showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Fator esperado", Grammar.descricao);
    }

    (
        (
            try{
                <OP_MULTIPLICACAO> | <OP_DIVISAO> | <OP_AND>
            }catch(ParseException e){
        
                showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Operador de multiplicação ou adição ou divisão esperado", Grammar.descricao);
            } 
        ) 
        try{
            fator()
        }
        catch(ParseException e){
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Fator esperado", Grammar.descricao);
        }
    )*
}


void fator() :
{}
{
    variavel() | 
    try{ 
        <NUMERO_INTEIRO> | <NUMERO_REAL>
    }catch(ParseException e){
        
        showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Número inteiro ou real esperado", Grammar.descricao);
    } 
    |
    (
        try{
            <PARENTESES_ABRE>
        }catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Abre parenteses esperado", Grammar.descricao);
        }  
        expressao()
        try{
            <PARENTESES_FECHA>
        }catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Fecha parenteses esperado", Grammar.descricao);
        } 
    ) 
    | 
    (
        try{
           <OP_NOT>
        }catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Operador not esperado", Grammar.descricao);
        }
        fator()
    )
    | 
    try{
        <TRUE> | <FALSE>
    }catch(ParseException e){
        
        showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Tipo de dado boleano(TRUE ou FALSE) esperado", Grammar.descricao);
    } 
}

void listaExpressoes() :
{}
{
    expressao() 

    (
        try{
            <VIRGULA>
        }catch(ParseException e){
        
            showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Virgula esperada", Grammar.descricao);
        }  
        expressao()
    )*
}


void variavel() :
{}
{
    try{
        <IDENTIFICADOR>
    }catch(ParseException e){
        
        showMessageError(e.currentToken.next.beginLine, e.currentToken.next.beginColumn, "Identificador esperado", Grammar.descricao);
    } 
}
